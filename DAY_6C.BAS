1000 REM Advent of Code 2022, Day 5 Part 1
1010 DEFINT A-Z: WSIZE=14: CR$=CHR$(13)
1030 GOTO 1290
1040 '
1050 REM Valid Tag W$ -> VALID
1060  VALID=0
1070  FOR I=1 TO WSIZE-1
1080   T$=MID$(W$,I,1)
1100   IF INSTR(I+1,W$,T$) THEN RETURN
1120  NEXT I
1130  VALID=1
1140 RETURN
1150 '
1160 REM Step Window W$ -> P,N
1165  IF EOF(1) THEN MID$(W$,P,1)=CR$: RETURN 
1170  MID$(W$,P,1)=INPUT$(1,1)
1175  IF P=WSIZE THEN P=1 ELSE P=P+1
1180  N=N+1
1185 RETURN
1190 '
1200 REM Prime Window -> W$, N, P, VALID
1205  IF EOF(1) THEN END
1210  W$=INPUT$(WSIZE,1)
1220  N=WSIZE: P=1: VALID=0
1230 RETURN
1240 '
1250 REM Skip Rest Of Signal
1260  LINE INPUT #1,T$
1270 RETURN
1280 '
1290 REM Main
1300  INPUT "File";F$
1310  OPEN F$ FOR INPUT AS #1
1320  WHILE NOT EOF(1)
1330   GOSUB 1200: REM Prime Window
1340   WHILE MID$(W$,P,1)<>CR$
1350    GOSUB 1050: REM Valid Tag?
1360    IF VALID THEN GOSUB 1250: GOTO 1410
1370    GOSUB 1160: REM Step Window
1400   WEND
1410   IF VALID THEN PRINT "MARKER=";N ELSE PRINT "NO MARKER FOUND"
1420  WEND
1430 END
