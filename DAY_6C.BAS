1000 REM Advent of Code 2022, Day 5 Part 2, String Version
1010 DEFINT A-Z: WSIZE=14: CR$=CHR$(13)
1020 GOTO 1300
1030 '
1040 REM Valid Tag W$ -> VALID
1050  VALID=0
1060  FOR I=1 TO WSIZE-1
1070   T$=MID$(W$,I,1)
1080   IF INSTR(I+1,W$,T$) THEN RETURN
1090  NEXT I
1100  VALID=1
1110 RETURN
1120 '
1130 REM Step Window W$ -> P,N
1140  IF EOF(1) THEN T$=CR$ ELSE T$=INPUT$(1,1)
1150  MID$(W$,P,1)=T$
1160  IF P=WSIZE THEN P=1 ELSE P=P+1
1170  N=N+1
1180 RETURN
1190 '
1200 REM Prime Window -> W$, N, P, VALID
1210  IF EOF(1) THEN END
1220  W$=INPUT$(WSIZE,1)
1230  N=WSIZE: P=1: VALID=0
1240 RETURN
1250 '
1260 REM Skip Rest Of Signal
1270  LINE INPUT #1,T$
1280 RETURN
1290 '
1300 REM Main
1310  INPUT "File";F$
1320  OPEN F$ FOR INPUT AS #1
1330  WHILE NOT EOF(1)
1340   GOSUB 1200: REM Prime Window
1350   WHILE MID$(W$,P,1)<>CR$
1360    GOSUB 1040: REM Valid Tag?
1370    IF VALID THEN GOSUB 1260: GOTO 1400
1380    GOSUB 1130: REM Step Window
1390   WEND
1400   IF VALID THEN PRINT "MARKER=";N ELSE PRINT "NO MARKER FOUND"
1410  WEND
1420 END
