1000 REM Advent of Code 2022, Day 9 - Rope Bridge, Part 1
1010 REM Solution = sort DAY_9.OUT | uniq | wc
1020 GOTO 1140: REM Main
1030 '
1040 REM Adjust Tail (TX,TY) relative to (HX,HY)
1050  DX=HX-TX: ADX=ABS(DX): DY=HY-TY: ADY=ABS(DY)
1060  IF DY=0 AND ADX>1 THEN TX=TX+SGN(DX): GOTO 1110
1070  IF DX=0 AND ADY>1 THEN TY=TY+SGN(DY): GOTO 1110
1080  IF ADX+ADY<=2 THEN RETURN
1090  TX=TX+SGN(DX): TY=TY+SGN(DY)
1100 '
1110  REM Record New Tail Position
1120  WRITE #2,TX,TY: RETURN
1130 '
1140 REM Main
1150  INPUT "File";F$
1160  PRINT TIME$,"Start"
1170  OPEN F$ FOR INPUT AS #1
1180  OPEN "DAY_9.OUT" FOR OUTPUT AS #2
1190  HX=0: HY=0: TX=0: TY=0
1200  GOSUB 1110: REM Record initial position
1210  WHILE NOT EOF(1)
1220   LINE INPUT #1,L$
1230   C$=LEFT$(L$,1): A%=VAL(MID$(L$,3))
1240   FOR I%=1 TO A%
1250    IF C$="U" THEN HY=HY+1: GOTO 1290
1260    IF C$="D" THEN HY=HY-1: GOTO 1290
1270    IF C$="L" THEN HX=HX-1: GOTO 1290
1280    IF C$="R" THEN HX=HX+1: GOTO 1290
1290    GOSUB 1040: REM Tail Chase
1300   NEXT I%
1310  WEND
1320  CLOSE#1: CLOSE#2
1330  PRINT TIME$,"End"
1340 END
