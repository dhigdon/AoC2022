1000 REM Advent of Code 2022, Day 4
1010 DEFINT A-Z
1098 GOTO 2000: REM Main
1099 '
1100 REM Split L$ into LO,HI
1110 I=INSTR(L$,"-")
1120 LO=VAL(MID$(L$,1,I)) : HI=VAL(MID$(L$,I+1))
1130 RETURN
1199 '
1200 REM Check Overlap(L1,H1,L2,H2), update O,P
1210 REM Rule out non-overlapping
1220 IF L1>H2 OR H1<L2 THEN RETURN
1230 IF L2>H1 OR H2<L1 THEN RETURN
1240 P=P+1
1250 REM Check for Total Overlap 
1260 IF L1<=L2 AND H1>=H2 THEN O=O+1: RETURN
1270 IF L2<=L1 AND H2>=H1 THEN O=O+1: RETURN
1300 RETURN
1310 '
2000 REM Main
2010 INPUT "File";F$
2020 OPEN F$ FOR INPUT AS #1
2030 WHILE NOT EOF(1)
2040  INPUT #1,A$,B$
2050  L$=A$: GOSUB 1100: L1=LO: H1=HI
2060  L$=B$: GOSUB 1100: L2=LO: H2=HI
2070  GOSUB 1200: REM Check Overlap(L1,H1,L2,H2)
2080 WEND
2090 CLOSE #1
2100 PRINT "Total Overlap=";O
2110 PRINT "Partial Overlap=";P
2120 END
