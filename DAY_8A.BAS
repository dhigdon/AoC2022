1000 REM Advent of Code 2022 Day 8 - Treetop Tree House
1010 DEFINT A-Z
1020 '
1030 INPUT "File";F$
1040 OPEN F$ FOR INPUT AS #1
1050 PRINT "Start ";TIME$
1060 LINE INPUT #1,L$
1070 SZ=LEN(L$)
1080 DIM GRID(SZ,SZ),VIS(SZ,SZ)
1090 FOR R=1 TO SZ
1100  GOSUB 2000: REM Load Row
1110  IF NOT EOF(1) THEN LINE INPUT #1,L$
1120 NEXT R
1130 PRINT "Size = ";SZ,TIME$
1140 '
1150 REM Set VIS(R,C) for visible trees
1160 FOR C=1 TO SZ
1170  VIS(C,1)=1: VIS(1,C)=1: VIS(C,SZ)=1: VIS(SZ,C)=1
1180 NEXT C
1190 REM Horizontal scan
1200 FOR R=2 TO SZ-1
1210  H=GRID(R,1): FOR C=2 TO SZ-1 STEP 1: GOSUB 3000: NEXT C
1220  H=GRID(R,SZ): FOR C=SZ-1 TO 2 STEP -1: GOSUB 3000: NEXT C
1230 NEXT R
1240 REM Vertical scan
1250 FOR C=2 TO SZ-1
1260  H=GRID(1,C): FOR R=2 TO SZ-1 STEP 1: GOSUB 3000: NEXT R
1270  H=GRID(SZ,C): FOR R=SZ-1 TO 2 STEP -1: GOSUB 3000: NEXT R
1280 NEXT C
1290 '
1300 REM REPORT
1310 FOR R=1 TO SZ
1320  FOR C=1 TO SZ: T=T+VIS(R,C): NEXT C
1330 NEXT R
1340 PRINT "Tree Count = ";T
1350 PRINT TIME$
1360 END
1370 '
2000 REM Load Row R with L$
2010  FOR I=1 TO SZ
2020   GRID(R,I)=VAL(MID$(L$,I,1))
2030  NEXT I
2040 RETURN
2050 '
3000 REM Accumulate VIS
3010 GRC=GRID(R,C)
3020 IF GRC>H THEN H=GRC: VIS(R,C)=1
3030 RETURN
