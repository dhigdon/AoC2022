1000 REM Advent of Code 2022, Day 5 Part 1
1010 DEFINT A-Z: WSIZE=14: DIM TAG(WSIZE)
1020 DEF FN EOS(X) = X=32 OR X=13
1030 GOTO 1290
1040 '
1050 REM Valid Tag TAG() -> VALID
1060 VALID=0
1070 FOR I=1 TO WSIZE-1
1080  TI=TAG(I)
1090  FOR J=I+1 TO WSIZE
1100   IF TI=TAG(J) THEN RETURN
1110  NEXT J
1120 NEXT I
1130 VALID=1
1140 RETURN
1150 '
1160 REM Rotate Window TAG()
1170 FOR I=2 TO WSIZE: TAG(I-1)=TAG(I): NEXT I
1180 RETURN
1190 '
1200 REM Prime Window -> TAG(), N
1210 FOR I=1 TO WSIZE: TAG(I)=ASC(INPUT$(1,1)): NEXT I
1220 N=WSIZE
1230 RETURN
1240 '
1250 REM Skip Rest Of Signal
1260 LINE INPUT #1,T$
1270 RETURN
1280 '
1290 REM Main
1300 INPUT "File";F$
1310 OPEN F$ FOR INPUT AS #1
1320 WHILE NOT EOF(1)
1330  GOSUB 1200: REM Prime Window
1340  WHILE NOT FN EOS(TAG(WSIZE))
1350   GOSUB 1050: REM Valid Tag?
1360   IF VALID THEN PRINT "MARKER=";N: GOSUB 1250: GOTO 1420
1370   GOSUB 1160: REM Rotate Window
1380   TAG(WSIZE)=ASC(INPUT$(1,1))
1390   N=N+1
1400  WEND
1410  PRINT "NO MARKER FOUND"
1420 WEND
1430 END
