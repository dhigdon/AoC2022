1000 REM Advent of Code 2022, Day 5 Part 1
1010 DEFINT A-Z: WSIZE=14: DIM TAG(WSIZE)
1020 DEF FN EOS(X) = X=32 OR X=13
1030 GOTO 1260
1040 '
1050 REM Valid Tag TAG() -> VALID
1060 VALID=0
1070 FOR I=1 TO WSIZE-1
1080  TI=TAG(I)
1090  FOR J=I+1 TO WSIZE
1100   IF TI=TAG(J) THEN RETURN
1110  NEXT J
1120 NEXT I
1130 VALID=1
1140 RETURN
1150 '
1160 REM Rotate Window TAG()
1170 FOR I=2 TO WSIZE
1180  TAG(I-1)=TAG(I)
1190 NEXT I
1200 RETURN
1210 '
1220 REM Skip Rest
1230 LINE INPUT #1,T$
1240 RETURN
1250 '
1260 REM Main
1270 INPUT "File";F$
1280 OPEN F$ FOR INPUT AS #1
1290 WHILE NOT EOF(1)
1300  FOR I=1 TO WSIZE: TAG(I)=ASC(INPUT$(1,1)): NEXT I: REM Prime
1310  N=WSIZE
1320  WHILE NOT FN EOS(TAG(WSIZE))
1330   GOSUB 1050: REM Valid Tag?
1340   IF VALID THEN PRINT "MARKER=";N: GOSUB 1220: GOTO 1400
1350   GOSUB 1160: REM Rotate Window
1360   TAG(WSIZE)=ASC(INPUT$(1,1))
1370   N=N+1
1380  WEND
1390  PRINT "NO MARKER FOUND"
1400 WEND
1410 END
