1000 REM Advent of Code 2022, Day 3
1005 REM Globals
1010 PRI$="abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
1020 DEF FN PR(C$)=INSTR(PRI$,C$)
1030 DIM SACK(52)
1040 GOTO 2000
1050 REM
1100 REM SUBROUTINE Tabulate(L$)
1105 FOR I = 1 TO 52: SACK(I) = 0: NEXT I
1106 RESULT = 0
1110 J = LEN(L$)\2
1120 FOR I = 1 TO J
1130  P = FN PR(MID$(L$,I,1))
1135  SACK(P) = SACK(P) + 1
1140 NEXT I
1150 FOR I = J+1 TO LEN(L$) 
1160  P = FN PR(MID$(L$,I,1))
1170  IF SACK(P) THEN RESULT = P
1180 NEXT I
1190 RETURN
1195 REM
2000 REM MAIN
2005 SUM = 0
2010 INPUT "File? ",F$
2020 OPEN F$ FOR INPUT AS #1
2030 IF EOF(1) THEN 3000
2040 LINE INPUT #1, L$
2050 GOSUB 1100: REM Tabulate(L$)
2060 SUM = SUM + RESULT
2070 GOTO 2030
2080 REM
3000 REM Report
3010 PRINT SUM
3020 CLOSE#1
3030 END
