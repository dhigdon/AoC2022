1010 REM Advent of Code 2022, Day 14, Part 1 - Regolith Reservoir
1020 REM
1030 REM From analyzing the data, the "working set" extends
1040 REM from 483,13 -> 569,166
1050 REM We'll choose a width for 100x170, which seems to fid
1100 REM It also seems that there is a LOT of overlap in the
1110 REM specified scan data.
1120 REM Field uses 0 for empty space, 1 for rock, and 2 for sand.
1130 REM
1140 DEFINT A-Z: ROWS=168: COLS=94: XORG=480: YORG=0: SAND=500-XORG
1150 DIM FLD(ROWS,COLS),BOT(COLS):K$=".#O"
1160 M=1: GOSUB 2000: REM read data
1170 M=2: GOSUB 4000: REM drop sand
1180 PRINT TIME$,"Grains Dropped=";COUNT
1190 GOSUB 3000: REM Dump field
1198 END
1199 '
1200 REM SUBROUTINE MARK(R,C)
1205  IF FLD(R,C)<>0 THEN RETURN
1206  ' PRINT "Mark";R;C;"=";M,
1210  FLD(R,C)=M
1220  IF M=1 AND R>BOT(C) THEN BOT(C)=R
1230 RETURN
1240 '
2000 REM Read the data and fill in the field
2010 INPUT "File";F$
2020 OPEN F$ FOR INPUT AS #1
2025 PRINT TIME$,"Reading ";F$
2030 WHILE NOT EOF(1)
2040  LINE INPUT #1,L$
2050  REM Parse X,Y[;X,Y]*
2070  SC=VAL(L$)-XORG
2080  N=INSTR(L$,",")
2090  SR=VAL(MID$(L$,N+1))-YORG
2110  N=INSTR(N+1,L$,";")
2120  IF N=0 THEN 2200
2140  EC=VAL(MID$(L$,N+1))-XORG
2150  N=INSTR(N+1,L$,",")
2160  ER=VAL(MID$(L$,N+1))-YORG
2170  IF SC=EC THEN C=SC: FOR R=SR TO ER STEP SGN(ER-SR): GOSUB 1200: NEXT R
2180  IF SR=ER THEN R=SR: FOR C=SC TO EC STEP SGN(EC-SC): GOSUB 1200: NEXT C
2190  SC=EC: SR=ER: GOTO 2110
2200 WEND
2210 CLOSE #1
2220 RETURN 
2999 '
3000 REM Dump Field
3005 PRINT TIME$,"Dump Field"
3010 OPEN "day14.map" FOR OUTPUT AS #1
3020 FOR R=1 TO ROWS
3040  FOR C=1 TO COLS
3050   IF R>BOT(C) THEN PRINT #1," "; ELSE PRINT #1,MID$(K$,FLD(R,C)+1,1);
3060  NEXT C: PRINT #1,""
3070 NEXT R
3080 CLOSE #1
3090 RETURN
3099 '
4000 REM Drop Sand until one falls off the world. COUNT them
4010 PRINT TIME$,"Pouring Sand..."
4020 C=SAND: R=1
4025 ' Scan down until we either fall off or hit something
4030 IF R>BOT(C) THEN RETURN
4040 R=R+1: IF FLD(R,C)=0 THEN GOTO 4030
4060 IF FLD(R,C-1)=0 THEN C=C-1: GOTO 4030
4070 IF FLD(R,C+1)=0 THEN C=C+1: GOTO 4030
4080 R=R-1: GOSUB 1200: COUNT=COUNT+1
4090 GOTO 4020
4100 RETURN
