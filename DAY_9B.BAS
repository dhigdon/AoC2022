1000 REM Advent of Code 2022, Day 9 - Rope Bridge, Part 2
1010 REM Solution = sort DAY_9.OUT | uniq | wc
1020 GOTO 1110: REM Main
1030 '
1040 REM Adjust Tail (TX,TY) relative to (HX,HY)
1050  DX=HX-TX: ADX=ABS(DX): DY=HY-TY: ADY=ABS(DY)
1060  IF DY=0 AND ADX>1 THEN TX=TX+SGN(DX): RETURN
1070  IF DX=0 AND ADY>1 THEN TY=TY+SGN(DY): RETURN
1080  IF ADX+ADY>2 THEN TX=TX+SGN(DX): TY=TY+SGN(DY)
1090 RETURN
1100 '
1110 REM Main
1120  INPUT "File";F$
1130  PRINT TIME$,"Start"
1140  OPEN F$ FOR INPUT AS #1
1150  OPEN "DAY_9.OUT" FOR OUTPUT AS #2
1160  HX=0: HY=0: TX=0: TY=0: DIM XS(10), YS(10)
1170  WRITE #2,TX,TY
1180  WHILE NOT EOF(1)
1190   LINE INPUT #1,L$
1200   C$=LEFT$(L$,1): A%=VAL(MID$(L$,3))
1210   FOR I%=1 TO A%
1220    HX=XS(1):HY=YS(1)
1230    IF C$="U" THEN HY=HY+1: GOTO 1270
1240    IF C$="D" THEN HY=HY-1: GOTO 1270
1250    IF C$="L" THEN HX=HX-1: GOTO 1270
1260    IF C$="R" THEN HX=HX+1: GOTO 1270
1270    XS(1)=HX:YS(1)=HY
1280    FOR J%=2 TO 10
1290     TX=XS(J%):TY=YS(J%)
1300     GOSUB 1040: REM Tail Chase
1310     XS(J%)=TX:YS(J%)=TY
1320     HX=TX: HY=TY
1330    NEXT J%
1340    WRITE #2,TX,TY
1350   NEXT I%
1360  WEND
1370  CLOSE#1: CLOSE#2
1380  PRINT ("run: sort DAY_9.OUT | uniq | wc -l")
1390  PRINT TIME$,"End"
1400 END
