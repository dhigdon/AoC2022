1000 REM Advent of Code 2022, Day 7 - No Space Left On Device
1010 DIM PATHS$(200): DIM PN#(200): DIM PT#(200): CWD$=""
1020 GOTO 1410
1030 '
1040 REM FindParent(T$) -> PARENT%
1050  PARENT%=1
1060  WHILE PARENT%<=NP% AND PATHS$(PARENT%)<>T$: PARENT%=PARENT%+1: WEND
1070 RETURN 
1080 '
1090 REM CD - Change CWD$ accordingly
1100  IF ARG$="/" THEN CWD$="!": RETURN
1110  IF ARG$=".." THEN CWD$=MID$(CWD$,INSTR(CWD$,"/")+1): RETURN
1120  CWD$=ARG$+"/"+CWD$
1130 RETURN
1140 '
1150 REM LS - Process all data from LS statement, Update PN(CWD)
1160  TOTAL#=0
1170  LINE INPUT #1,L$
1180  WHILE INSTR(L$,"$")=0
1190   TOTAL#=TOTAL#+VAL(L$): REM "dir x" will have val=0
1200   IF NOT EOF(1) THEN LINE INPUT #1,L$ ELSE L$="$"
1210  WEND
1220  REM Save file total (note, ls commands don't repeat folders)
1230  NP%=NP%+1: PATHS$(NP%)=CWD$: PN#(NP%)=TOTAL#
1240 RETURN
1250 '
1260 REM Tabulate
1270  FOR I%=1 TO NP%
1280   CWD$=PATHS$(I%):TOTAL#=PN#(I%)
1290   PT#(I%)=PN#(I%)
1300   J%=INSTR(CWD$,"/")
1310   WHILE J%>0
1320    T$=MID$(CWD$,J%+1)
1330    GOSUB 1040: REM PARENT% = FindParent(T$)
1340    IF PARENT%=0 THEN PRINT T$;" not found": END
1350    PT#(PARENT%)=PT#(PARENT%)+PN#(I%)
1360    J%=INSTR(J%+1,CWD$,"/")
1370   WEND
1380  NEXT I%
1390 RETURN
1400 '
1410 REM Main
1420  INPUT "File";F$
1430  OPEN F$ FOR INPUT AS #1
1440  LINE INPUT #1,L$
1450  WHILE NOT EOF(1)
1460   X%=INSTR(L$," ")
1470   CMD$=LEFT$(L$,X%-1)
1480   IF CMD$<>"$" THEN 1290: REM SKIP DATA
1490   CMD$=MID$(L$,3,2):X%=INSTR(3,L$," ")
1500   IF X%=0 THEN ARG$="" ELSE ARG$=MID$(L$,X%+1)
1510   IF CMD$="cd" THEN GOSUB 1090: LINE INPUT #1,L$
1520   IF CMD$="ls" THEN GOSUB 1150: REM Reads next line
1530  WEND
1540  CLOSE #1
1550  '
1560  GOSUB 1260: REM Tabulate
1570  TOTAL=0
1580  FOR I%=1 TO NP%
1590   IF PT#(I%)<=100000! THEN TOTAL=TOTAL+PT#(I%)
1600  NEXT I%
1610  PRINT "Total Under 100000 = ";TOTAL
1620  '
1630  REM Part 2 - find smallest path we could delete
1640  MX#=PT#(1): X#=1: FS#=70000000#-MX#: GOAL#=30000000#: FIND#=GOAL#-FS#
1650  PRINT "Free Space ";FS#
1660  PRINT "Need to free up ";FIND#
1670  FOR I%=1 TO NP%
1680   D#=PT#(I%)
1690   IF D#>=FIND# AND D#<MX# THEN MX#=D#
1700  NEXT I%
1710  PRINT "Smallest = ";MX#
1720 END
