1000 REM Advent of Code 2022, Day 3
1005 REM Globals
1010 PRI$="abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
1020 DEF FN PR(C$)=INSTR(PRI$,C$)
1030 DIM A(52),B(52)
1040 GOTO 2000
1050 REM
1100 REM SUBROUTINE Load A
1110 FOR I = 1 TO 52: A(I) = 1: NEXT I
1120 RETURN
1130 REM
1200 REM SUBROUTINE Sift A
1205 FOR i = 1 TO 52: B(I) = 0: NEXT I
1210 FOR I = 1 TO LEN(L$)
1220  P = FN PR(MID$(L$,I,1))
1230  IF A(P) THEN B(P) = B(P) + 1
1240 NEXT I
1250 RETURN
1260 REM
1300 REM SUBROUTINE Sift B
1305 FOR i = 1 TO 52: A(I) = 0: NEXT I
1310 FOR I = 1 TO LEN(L$)
1320  P = FN PR(MID$(L$,I,1))
1330  IF B(P) THEN A(P) = A(P) + 1
1340 NEXT I
1350 RETURN
1360 REM
2000 REM MAIN
2005 SUM = 0
2010 INPUT "File? ",F$
2020 OPEN F$ FOR INPUT AS #1
2030 IF EOF(1) THEN 3000
2040 GOSUB 1100: REM Load A
2050 LINE INPUT #1, L$: GOSUB 1200: REM Sift A
2060 LINE INPUT #1, L$: GOSUB 1300: REM Sift B
2070 LINE INPUT #1, L$: GOSUB 1200: REM Sift A 
2080 FOR I = 1 TO 52: IF B(I) THEN SUM = SUM + I: NEXT I
2090 GOTO 2030
3000 REM Report
3010 PRINT "TOTAL=";SUM
3020 CLOSE#1
3030 END
