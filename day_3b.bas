1000 REM Advent of Code 2022, Day 3, Part 2
1010 REM Globals
1020 DEFINT A-Z
1030 PRI$="abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
1040 DEF FN PR(S$,I)=INSTR(PRI$,MID$(S$,I,1))
1050 DIM A(52)
1060 GOTO 2000: REM Start Program
1070 '
1100 REM SUBROUTINE Load
1110 GEN = 0
1120 FOR I = 1 TO 52: A(I) = GEN: NEXT I
1130 RETURN
1140 '
1200 REM SUBROUTINE Sift L$
1210 NGEN = GEN + 1: J = LEN(L$)
1220 FOR I = 1 TO J 
1230  P = FN PR(L$,I)
1240  IF A(P) = GEN THEN A(P) = NGEN
1250 NEXT I
1260 GEN = NGEN
1270 RETURN
1280 '
2000 REM MAIN
2010 INPUT "File? ",F$
2020 OPEN F$ FOR INPUT AS #1
2030 WHILE NOT EOF(1)
2040  GOSUB 1100: REM Load
2050  LINE INPUT #1, L$: GOSUB 1200: REM Sift
2060  LINE INPUT #1, L$: GOSUB 1200: REM Sift
2070  LINE INPUT #1, L$: GOSUB 1200: REM Sift
2080  FOR I = 1 TO 52
2090   IF A(I)=GEN THEN SUM = SUM + I
2100  NEXT I
2110 WEND
2120 '
3000 REM Report
3010 PRINT "TOTAL=";SUM
3020 CLOSE#1
3030 END
